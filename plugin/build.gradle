plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.1.4-3'
    id 'org.jetbrains.dokka' version '0.9.15'

    id 'java-gradle-plugin'
    id 'java-library'
    id 'maven-publish'
}

dependencies {
    implementation project(':library')

    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jre8:1.1.4-3'

    implementation 'com.squareup.okhttp3:okhttp:3.9.0'
    implementation 'org.dom4j:dom4j:2.1.0'
    implementation 'org.xmlunit:xmlunit-core:2.5.0'
    implementation 'nu.studer:java-ordered-properties:1.0.1'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.amshove.kluent:kluent-android:1.29'
}

task sourceJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: dokka) {
    from javadoc.destinationDir
}

dokka {
    jdkVersion = 8
    outputDirectory = javadoc.destinationDir

    externalDocumentationLink {
        url = new URL("https://dom4j.github.io/javadoc/2.1.0/")
    }
}

gradlePlugin {
    plugins {
        squit {
            id = "squit"
            implementationClass = "de.smartsquare.squit.SquitPlugin"
        }
    }
}

publishing {
    repositories {
        maven {
            credentials {
                username "${if (project.hasProperty("nexusUser")) project.property("nexusUser") else 'dummy'}"
                password "${if (project.hasProperty("nexusPassword")) project.property("nexusPassword") else 'dummy'}"
            }

            url "https://nexus.smartsquare.de/content/repositories/snapshots/"
        }
    }

    publications {
        mavenJava(MavenPublication) {
            artifactId 'squit-plugin'

            from components.java

            artifact sourceJar {
                classifier 'sources'
            }

            artifact javadocJar {
                classifier "javadoc"
            }
        }
    }
}
