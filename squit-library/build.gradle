buildscript {
    apply from: '../gradle/versions.gradle'

    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:$bintrayPublishVersion"

        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokkaVersion"

        classpath "gradle.plugin.org.jlleitschuh.gradle:ktlint-gradle:$ktlintPluginVersion"
        classpath "gradle.plugin.io.gitlab.arturbosch.detekt:detekt-gradle-plugin:$detektVersion"
    }
}

apply plugin: 'com.jfrog.bintray'

apply plugin: 'java-library'
apply plugin: 'maven-publish'

apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.dokka'

apply plugin: 'io.gitlab.arturbosch.detekt'
apply plugin: 'org.jlleitschuh.gradle.ktlint'

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"

    api "org.dom4j:dom4j:$dom4jVersion"
}

task sourceJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: dokka) {
    from javadoc.destinationDir
}

ktlint {
    version = ktlintVersion
    reporters = ["CHECKSTYLE"]
}

detekt {
    profile("main") {
        config = "${rootProject.projectDir}/config/detekt/detekt.yml"
        output = "$buildDir/reports/detekt"
    }
}

dokka {
    jdkVersion = 8
    outputDirectory = javadoc.destinationDir

    externalDocumentationLink {
        url = new URL("https://dom4j.github.io/javadoc/$dom4jVersion/")
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['mavenJava']

    pkg {
        repo = 'squit'
        name = 'squit-library'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/SmartsquareGmbH/squit'
        labels = ['testing', 'soap', 'xml', 'automation']
        publicDownloadNumbers = true
        version {
            name = squitVersion
            desc = 'Gradle plugin for testing Xml/Soap based apis.'
            vcsTag = squitVersion
        }
    }

}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId 'squit-library'

            from components.java

            artifact sourceJar {
                classifier 'sources'
            }

            artifact javadocJar {
                classifier "javadoc"
            }
        }
    }
}
