import org.jetbrains.kotlin.gradle.tasks.KotlinCompile
import org.jlleitschuh.gradle.ktlint.reporter.ReporterType

buildscript {
    apply from: "./gradle/versions.gradle"

    repositories {
        gradlePluginPortal()
        jcenter()
    }

    dependencies {
        classpath "com.github.ben-manes:gradle-versions-plugin:$gradleVersionsVersion"
        classpath "com.gradle.publish:plugin-publish-plugin:$gradlePublishVersion"

        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokkaVersion"

        classpath "org.jlleitschuh.gradle:ktlint-gradle:$ktlintPluginVersion"
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:$detektVersion"
    }
}

apply plugin: "com.github.ben-manes.versions"

apply plugin: "com.gradle.plugin-publish"
apply plugin: "java-gradle-plugin"
apply plugin: "java-library"
apply plugin: "maven-publish"
apply plugin: "jacoco"

apply plugin: "kotlin"
apply plugin: "org.jetbrains.dokka"

apply plugin: "io.gitlab.arturbosch.detekt"
apply plugin: "org.jlleitschuh.gradle.ktlint"

group = "de.smartsquare"
version = squitVersion

sourceCompatibility = 1.8

repositories {
    jcenter()
}

configurations {
    jacocoRuntimeOnly
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-html-jvm:$kotlinHtmlVersion"

    api "org.dom4j:dom4j:$dom4jVersion"
    api "com.google.code.gson:gson:$gsonVersion"
    api "com.typesafe:config:$typesafeConfigVersion"

    implementation "jaxen:jaxen:$jaxenVersion"
    implementation "org.apache.santuario:xmlsec:$xmlSecVersion"
    implementation "org.xmlunit:xmlunit-core:$xmlUnitVersion"
    implementation "net.javacrumbs.json-unit:json-unit:$jsonUnitVersion"
    implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"
    implementation "se.sawano.java:alphanumeric-comparator:$alphanumericComparatorVersion"
    implementation("com.github.wumpz:diffutils:$diffUtilsVersion") {
        exclude group: "org.eclipse.jgit", module: "*"
    }

    implementation "org.webjars.npm:jquery:$jqueryVersion"
    implementation "org.webjars.npm:bootstrap:$bootstrapVersion"
    implementation "org.webjars.npm:popper.js:$popperVersion"
    implementation "org.webjars:font-awesome:$fontAwesomeVersion"
    implementation "org.webjars.npm:marked:$markedVersion"
    implementation("org.webjars.npm:diff2html:$diff2htmlVersion") {
        exclude group: "org.webjars.npm", module: "*"
    }

    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    testImplementation "org.jetbrains.spek:spek-api:$spekVersion"
    testImplementation "org.jetbrains.spek:spek-subject-extension:$spekVersion"
    testImplementation "org.junit.platform:junit-platform-runner:$junitPlatformVersion"
    testImplementation "org.amshove.kluent:kluent:$kluentVersion"
    testImplementation "com.squareup.okhttp3:mockwebserver:$okhttpVersion"
    testImplementation "com.h2database:h2:$h2Version"
    testImplementation "io.mockk:mockk:$mockkVersion"

    testRuntimeOnly "org.jetbrains.spek:spek-junit-platform-engine:$spekVersion"

    jacocoRuntimeOnly "org.jacoco:org.jacoco.agent:${jacoco.toolVersion}:runtime"
}

sourceSets {
    test {
        resources {
            srcDir "$buildDir/testkit"
        }
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

tasks.withType(KotlinCompile).all {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        allWarningsAsErrors = true
        jvmTarget = "1.8"
    }
}

test {
    useJUnitPlatform()
}

jacoco {
    toolVersion = jacocoVersion
}

ktlint {
    disabledRules = ["import-ordering"]

    reporters {
        reporter ReporterType.CHECKSTYLE
    }
}

detekt {
    config = files("${rootProject.projectDir}/detekt.yml")
    input = files("$projectDir/src/main/kotlin")
    buildUponDefaultConfig = true

    reports {
        xml.destination = file("$buildDir/reports/detekt/detekt.xml")
        html.destination = file("$buildDir/reports/detekt/detekt.html")
    }
}

dokkaHtml {
    outputDirectory.set(javadoc.destinationDir)

    dokkaSourceSets {
        named("main") {
            jdkVersion.set(8)

            sourceLink {
                localDirectory.set(file("src/main/kotlin"))
                remoteUrl.set(new URL("https://github.com/SmartsquareGmbH/squit/blob/master/src/main/kotlin"))
                remoteLineSuffix.set("#L")
            }

            externalDocumentationLink {
                url.set(new URL("https://dom4j.github.io/javadoc/$dom4jVersion/"))
                packageListUrl.set(new URL("https://dom4j.github.io/javadoc/$dom4jVersion/package-list"))
            }

            externalDocumentationLink {
                url.set(new URL("https://lightbend.github.io/config/latest/api"))
                packageListUrl.set(new URL("https://lightbend.github.io/config/latest/api/package-list"))
            }

            externalDocumentationLink {
                url.set(new URL("https://www.javadoc.io/doc/com.google.code.gson/gson/$gsonVersion/com.google.gson"))
                packageListUrl.set(new URL("https://raw.githubusercontent.com/google/gson/gson-parent-$gsonVersion/gson/docs/javadocs/package-list"))
            }
        }
    }
}

dependencyUpdates {
    rejectVersionIf {
        isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
    }
}

static def isNonStable(String version) {
    def stableKeyword = ["RELEASE", "FINAL", "GA"].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/

    return !stableKeyword && !(version ==~ regex)
}

tasks.withType(Javadoc).all {
    it.dependsOn dokkaHtml
}

pluginBundle {
    website = "https://github.com/SmartsquareGmbH/squit"
    vcsUrl = "https://github.com/SmartsquareGmbH/squit"
    description = "Gradle plugin for simple testing of JSON/XML/SOAP/etc APIs."
    tags = ["testing", "json", "xml", "soap", "automation"]

    plugins {
        squit {
            id = "de.smartsquare.squit"
            displayName = "Squit"
        }
    }
}

gradlePlugin {
    plugins {
        squit {
            id = "de.smartsquare.squit"
            implementationClass = "de.smartsquare.squit.SquitPlugin"
        }
    }
}

wrapper {
    gradleVersion = project.ext.gradleVersion
}

task generateTestkitFiles {
    def jacocoPath = configurations.jacocoRuntimeOnly.asPath.toString().replace("\\", "/")
    def jvmArgs = "org.gradle.jvmargs=-javaagent:$jacocoPath=destfile=$buildDir/jacoco/test.exec"
    def outputDir = file("$buildDir/testkit")

    inputs.files jacocoPath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("$outputDir/testkit-gradle.properties").text = jvmArgs
    }
}

tasks.named("processTestResources") {
    dependsOn generateTestkitFiles
}

gradle.projectsEvaluated {
    test {
        dependsOn generateTestkitFiles
        finalizedBy jacocoTestReport
    }
}
